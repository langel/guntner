
oam_ram_rudy		EQU $0204
player_dead_tile	EQM $0a


player_game_reset: subroutine
	lda #$00
        sta phase_state
        sta phase_current
        sta phase_level
        sta phase_kill_counter
        sta phase_end_game
        sta player_death_flag
        sta bomb_counter
        sta r_bag_counter
        sta shroom_counter
        sta mask_shield
        sta you_dead_counter
        sta score_xx000000
        sta score_00xx0000
        sta score_0000xx00
        sta score_000000xx
        ; XXX testing phase behaviours
        lda #$0
        sta phase_current
        ;lda #1
        ;sta player_boundless
        ;lda #1
        ;sta phase_level
        sta phase_arctang_counter
        jsr phase_palette_load
        jsr timer_reset
        ; XXX testing boss spawn stuff
	lda #$d0
	;lda #$a0
        sta player_x_hi
        sta player_demo_x
	lda #$70
        sta player_y_hi
        sta player_demo_y
        lda #$ff
        sta player_health
; set player stats
	lda #$03
        sta player_lives
        ldx game_difficulty
        lda player_gun_strength_per_difficulty,x
        sta player_gun_str
        lda #$08
        sta player_autofire_s
        rts
        
  
        
        
player_update_colors: subroutine
	; dashboard is bg_3
        ; player is spr_0
	lda player_color0
        sta pal_spr_0_3
        ; darken dash bg color
        sec
        sbc #$10
        sta pal_bg_3_1
        ; do the rest of player colours
	lda player_color1
        sta pal_bg_3_2
        sta pal_spr_0_2
	lda player_color2
        sta pal_bg_3_3
        sta pal_spr_0_1
	rts
        
        
    
player_difficulty_damage_table:
	byte #58, #77, #109, #191
        
player_take_damage: subroutine
	; make sure iframes are not enable
        lda state_iframes
        beq .not_iframes
        rts
.not_iframes
        lda shroom_counter
        beq .apply_damage
        rts
.apply_damage
	; check if turbo gun is enabled
        lda player_a
        bne .skip_iframes
	; set iframes
        lda #8
        sta state_iframes
.skip_iframes
	; damage amount in player_damage
	lda player_health
        sec
        sbc player_damage
        bcc .player_dead
        sta player_health
        rts
.player_dead
	; player can be saved by mask shield!
        lda mask_shield
        beq .player_really_dead
        lda #$00
        sta mask_shield
        lda #2
        sta bullet_x_vel
        lda #$ff
        sta player_health
        rts
.player_really_dead
	; player_health 00 means DEATH
	lda #$00
        sta player_health
        rts
         
        

        
        
set_player_sprite: subroutine
	; sprite
        lda #$8e
        sta oam_ram_rudy+5
        lda #$8f
        sta oam_ram_rudy+1
        ; x pos
        lda player_x_hi
        sta $207
        clc
        adc #$08
        sta $20b
        ; attributes
        lda #$40
        sta $206
        sta $20a
        
        lda player_health
        bne .not_dead
.is_dead
	lda #player_dead_tile
        sta oam_ram_rudy+5
        sta oam_ram_rudy+1
        rts
.not_dead
	; check and decrement iframe counter
	lda state_iframes
        beq .not_iframes
        dec state_iframes
        ; flash rudy based on iframe counter
        lsr
        lsr
        and #$01
        beq .not_iframes
        ; y pos
	lda #$ff
        sta $204
        sta $208
        rts
.not_iframes
        ; y pos
	lda player_y_hi
        sta $204
        sta $208
        rts
        