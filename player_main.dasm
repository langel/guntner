
oam_ram_rudy		EQU $0204

; CONST
player_dead_tile	EQM $c0
iframe_attract_length	EQM $08
iframe_game_length	EQM $04


player_game_reset: subroutine
	lda #$00
        sta ftw
        ; clearing 18 bytes of game data
        ; see zp_vars.asm for details! ;D
        ldx #$20
.clear_loop
	sta phase_state,x
        dex
        bpl .clear_loop
        ; XXX testing phase behaviours
        ;lda #3
        ;sta phase_level
        ;lda #$3f
        ;sta phase_current
        jsr phase_palette_load
        jsr timer_reset
	lda #$d0
        sta player_x_hi
        sta player_demo_x
	lda #$70
        sta player_y_hi
        sta player_demo_y
        lda #$ff
        sta player_health
; set player stats
	lda #$03
        sta player_lives
        ldx game_difficulty
        lda player_gun_strength_per_difficulty,x
        sta player_gun_str
        lda #$0f
        sta player_autofire_s
        rts
        
  
        
        
player_update_colors: subroutine
	; dashboard is bg_3
        ; player is spr_0
	lda player_color0
        sta pal_spr_0_3
        ; darken dash bg color
        sec
        sbc #$10
        sta pal_bg_3_1
        ; do the rest of player colours
	lda player_color1
        sta pal_bg_3_2
        sta pal_spr_0_2
	lda player_color2
        sta pal_bg_3_3
        sta pal_spr_0_1
	rts
        
        
        
player_take_damage: subroutine
	; make sure iframes are not enable
        lda state_iframes
        beq .not_iframes
        rts
.not_iframes
        lda shroom_counter
        beq .apply_damage
        rts
.apply_damage
	; set damage display counter
        lda #$10
        sta player_damage_flash
	; check if turbo gun is enabled
        lda player_a
        bne .skip_iframes
	; set iframes
        lda state_iframe_length
        sta state_iframes
.skip_iframes
	; damage amount in player_damage
	lda player_health
        sec
        sbc player_damage
        bcc .player_dead
        beq .player_dead
        sta player_health
        rts
.player_dead
	; player can be saved by mask shield!
        lda mask_shield
        beq .player_really_dead
        lda #$00
        sta mask_shield
        lda #1
        sta bullet_x_vel
        lda #$ff
        sta player_health
        rts
.player_really_dead
	; player_health 00 means DEATH
	lda #$00
        sta player_health
        rts
         
        

        
        
set_player_sprite: subroutine
	lda state_render_addr
        cmp #state_render_jump_table_offset+1
        bne .not_menu_screens
.is_menu_screens
        ; sprite (menus)
        lda #$8e
        sta oam_ram_rudy+1
        lda #$8f
        sta oam_ram_rudy+5
        bne .sprite_set
.not_menu_screens
	; sprite (game)
        lda #$4a
        sta oam_ram_rudy+1
        lda #$4b
        sta oam_ram_rudy+5
.sprite_set
        ; x pos
        lda player_x_hi
        sta $207
        clc
        adc #$08
        sta $20b
        ; y pos
	lda player_y_hi
        sta $204
        sta $208
        ; attributes
        lda #$00
        sta $206
        sta $20a
        
        lda player_health
        bne .not_dead
.is_dead
	lda #player_dead_tile
        sta oam_ram_rudy+5
        sta oam_ram_rudy+1
        rts
.not_dead
	; check and decrement iframe counter
	lda state_iframes
        beq .not_iframes
        dec state_iframes
.not_iframes
	; check and decrement iframe counter
	lda player_damage_flash
        beq .not_flashing
        dec player_damage_flash
        ; flash rudy based on iframe counter
        lsr
        and #$01
        bne .not_flashing
        ; iframes
        lda #$4c
        sta oam_ram_rudy+1
        lda #$4d
        sta oam_ram_rudy+5
        rts
.not_flashing
        rts
        