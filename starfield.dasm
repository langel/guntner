;;;; Constants

STARFIELD_MASK		.byte #%00111111 ; star probability
STARFIELD_COL0_START	.byte #$00
STARFIELD_COL1_START	.byte #$07

starfield_tile	.byte #$0f
starempty_tile	.byte #$20

star_cache	EQU 	$0140


starfield_init: subroutine
; set bg tile palette attributes / colors
; $23c0 and $27c0
        ; page 1 attributes
	PPU_SETADDR $23c0
	lda #%00010100
        ldx #$e0
.23c0_loop
        sta PPU_DATA
        inx
        bne .23c0_loop
        ; page 2 attributes
	PPU_SETADDR $27c0
	lda #%00010100
        ldx #$e0
.27c0_loop
        sta PPU_DATA
        inx
        bne .27c0_loop
; reset star palette twinkles
	lda STARFIELD_COL0_START
        sta starfield_col0
        lda STARFIELD_COL1_START
        sta starfield_col1
        ; XXX should go ahead and fill in all star tiles
        ; before render
	rts
        
        
death_scroll_speed:
        lda scroll_speed
        cmp #$00
        beq .scroll_slow_done
        dec scroll_speed
	dec scroll_speed_m
        lda scroll_speed_m
        lsr
        lsr
        lsr
        sta scroll_speed
        ; slow down stars
.scroll_slow_done
	rts
        
        
starfield_render: subroutine
	; copy tiles to PPU VRAM during vblank
;update_starfield_column
        lda #CTRL_INC_32
        sta PPU_CTRL
        ; set base PPU address
	lda starfield_page
        sta PPU_ADDR
        lda starfield_col
        sta PPU_ADDR
; rip from cache to PPU
        ldy #$16
.col_loop
	lda star_cache,y
        sta PPU_DATA
        dey
        bne .col_loop
        rts



starfield_update: subroutine
	; precalculate all starfield things post sprite 0
	
	; update scroll pos
	lda scroll_y
        sec
        sbc scroll_speed
        bcs .samepage
        inc scroll_page
.samepage
	sta scroll_y
	
        ; twinkle them stars with palette stuff
        lda starfield_col0
        jsr palette_next_rainbow_color
        sta starfield_col0
        sta pal_bg_0_1
        clc
        adc #$21
        sta pal_bg_0_2
        adc #$0d
        sta pal_bg_0_3
        lda starfield_col1
        jsr palette_next_rainbow_color
        sta starfield_col1
        sta pal_bg_1_1
        clc
        adc #$21
        ;sta pal_bg_1_2
        adc #$0d
        sta pal_bg_1_3
        

	; XXX hook this up with tile changing later
        ; probably hearts after killing a boss
        ; XXX starfield_tile
        ; #$0f star
        ; #$90 heart

        
	lda scroll_y
        lsr
        lsr
        lsr
        sta starfield_col
        
        lda scroll_page
        and #$01
        cmp #$00
        bne .scroll_page
        lda #$20
        sta starfield_page
   	jmp .scroll_page_done
.scroll_page
	lda #$24
        sta starfield_page
.scroll_page_done
        
        ; setup rng for star probability
        lda rng0
        lsr
        and STARFIELD_MASK
        sta starfield_rng
      	ldy #$00
.col_assign_tile_loop
	cpy starfield_rng
        bne .empty_tile
.star_tile
	lda starfield_tile
        jmp .cache_tile
.empty_tile
	lda starempty_tile 
.cache_tile
	sta star_cache,y
        iny
        cpy #$16
        bne .col_assign_tile_loop
        rts
        
        
        
        