
        
player_demo_init: subroutine
; redraw playfield/hud
	; disable rendering
        lda #$00
        sta PPU_MASK	
        jsr PPU_init
; clear top rows
	PPU_SETADDR $2000
        ldy #$00
        lda #$20
.clear_top_rows
	sta PPU_DATA
        iny
        bne .clear_top_rows
	jsr starfield_init
        jsr dashboard_init
        sta PPU_ADDR
        sta PPU_ADDR	; PPU addr = $0000
        sta PPU_SCROLL
        sta PPU_SCROLL  ; PPU scroll = $0000
	; enable rendering
        lda #MASK_BG|MASK_SPR
        sta PPU_MASK	
        jsr timer_reset
; set player lives
	lda #$05
        sta player_lives
; set player position
	lda #$00
        sta player_death_flag
        sta you_dead_counter
        sta phase_current
        sta score_00
        sta score_00__
        sta score_00____
        sta score_00______
	sta timer_frames_1s	
	sta timer_frames_10s	
	sta timer_seconds_1s	
	sta timer_seconds_10s	
	sta timer_minutes_1s	
	sta timer_minutes_10s	
	lda #$d0
        sta player_x_hi
        sta player_demo_x
	lda #$70
        sta player_y_hi
        sta player_demo_y
        lda #$ff
        sta player_health
  ; SCROLL SPEED
  	lda #$07
        sta scroll_speed
        asl
        asl
        asl
        sta scroll_speed_m
        
        ldx #$8f ; set tiles
        stx $205
        dex 
        stx $209
        rts


demo_mode: subroutine
	; read user controls even in demo mode!
	jsr player_change_speed
        
        ; start should return to menu in demo mode
        lda player_start_d
        cmp #$ff
        bne .player_check_for_dead
        jsr title_screen_init
        jmp .done_and_paused
        
.player_check_for_dead
	; MOCKUP DEATH SEQUENCE
        lda player_health
        cmp #$00
        bne .player_not_dead
.player_dead
	jmp .player_dead_anim
        
.player_not_dead
	; push the shoot button
        lda timer_frames_10s
        and #$02
        cmp #$02
        bne .dont_shoot
	lda #$ff
        sta player_b_d
        ; spawn enemies
        jsr demo_enemy_spawn
.dont_shoot
        jsr set_player_sprite
; demo flight
	jsr run_player_demo
        
;; XXX FORCE QUICK DEATH
        lda #$04
        sta player_damage
        ;jsr player_take_damage
        
        jmp .done
        
.player_dead_anim
	;jsr player_bullets_despawn
        jsr death_scroll_speed
        lda player_death_flag
        cmp #$00
        bne .death_already_set
        jsr apu_trigger_player_death
        lda #$01
        sta player_death_flag
        jsr dashboard_you_dead
        jmp .done
.death_already_set
	inc you_dead_counter
        lda you_dead_counter
        cmp #120
        bne .still_dead
        
        ; GO BACK TO TITLE SCREEN 
        ; AFTER DEATH SEQUENCE
        jsr title_screen_init
.still_dead
.done
	jsr player_bullets_demo_update
        jsr update_enemies
        jsr apu_game_frame
.done_and_paused
	rts
        
        
        
        
demo_enemy_spawn: subroutine
	jsr get_enemy_slot_1_sprite
        cmp #$ff
        beq .no_birb_spawn
        tax
        jsr birb_spawn_demo
.no_birb_spawn
	jsr get_enemy_slot_2_sprite
        cmp #$ff
        beq .no_maggs_spawn
        tax
        jsr maggs_spawn
.no_maggs_spawn
	jsr get_enemy_slot_4_sprite
        cmp #$ff
        beq .no_bigs_spawn
        tax
        lda rng0
        jsr NextRandom
        and #$01
        cmp #$00
        beq .spawn_starglasses
        jsr skully_spawn
        jmp .no_bigs_spawn
.spawn_starglasses
        jsr starglasses_spawn
.no_bigs_spawn
	rts
        
        


        
        
; controls player in demo mode    
run_player_demo: subroutine
	; clear player directions
        lda #$00
        sta player_dir_bits
        ; set flags in y register
        ldy #$00
; check x coordinate
        lda player_x_hi
        cmp player_demo_x
        beq .player_x_equal
        bcs .player_x_greater
.player_x_lesser
	lda player_demo_lr
        cmp #$ff
        beq .player_x_equal
	; go right
        lda #%00000001
        ora player_dir_bits
        sta player_dir_bits
        ;inc player_x_hi
        jmp .player_x_done
.player_x_greater
	lda player_demo_lr
        cmp #$00
        beq .player_x_equal
	; go left
        lda #%00000010
        ora player_dir_bits
        sta player_dir_bits
	;dec player_x_hi
        jmp .player_x_done
.player_x_equal
	iny
.player_x_done
; check y coordinate
        lda player_y_hi
        cmp player_demo_y
        beq .player_y_equal
        bcs .player_y_greater
.player_y_lesser
	lda player_demo_ud
        cmp #$ff
        beq .player_y_equal
	; go down
        lda #%00000100
        ora player_dir_bits
        sta player_dir_bits
        ;inc player_y_hi
        jmp .player_y_done
.player_y_greater
	lda player_demo_ud
        cmp #$00
        beq .player_y_equal
	; go up
        lda #%00001000
        ora player_dir_bits
        sta player_dir_bits
	;dec player_y_hi
        jmp .player_y_done
.player_y_equal
	iny
.player_y_done
; check if both coordinates are met
	cpy #$02
        beq .set_demo_new_target
        jmp .done
.set_demo_new_target

	; set x target
	lda rng0
        jsr NextRandom
        sta rng0
        lsr
        clc
        adc #$60
        sta player_demo_x
        cmp player_x_hi
        bcs .player_going_left
.player_going_right
        lda #$ff
        sta player_demo_lr
        jmp .player_x_dir_done
.player_going_left
	lda #$00
        sta player_demo_lr
.player_x_dir_done
        
        ; set y target
	lda rng0
        jsr NextRandom
        sta rng0
        lsr
        clc
        adc #$10
        sta player_demo_y
        cmp player_y_hi
        bcs .player_going_up
.player_going_down
        lda #$ff
        sta player_demo_ud
        jmp .player_y_dir_done
.player_going_up
	lda #$00
        sta player_demo_ud
.player_y_dir_done

.done
	;lda #%0000110
        ;sta player_dir_bits
	jsr player_move_position
	rts

        
        


