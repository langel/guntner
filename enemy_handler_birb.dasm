


birb_spawn: subroutine
	; x is set by enemy spawner
	lda #$02
        sta $0300,x ; enemy type
        lda #$00
        sta $0301,x ; x pos
        sta $0303,x ; pattern counter
        lda rng0
        jsr NextRandom
        sta rng0
        sta $0304,x ; animation counter
        tay
        lda game_height_scale,y
        sta $0302,x ; y pos
        lda #$01
        sta $0305,x ; health
        clc
        txa
        lsr
        adc #$20
        sta $0307,x ; OAM ref  
   	rts
        
        
        

birb_spawn_demo: subroutine
	; x is set by enemy spawner
	lda #$02
        sta $0300,x ; enemy type
        lda #$00
        sta $0301,x ; x pos
        sta $0303,x ; pattern counter
        lda rng0
        sta $0304,x ; animation counter
        lsr
        sta $0302,x ; y pos
        lda #$01
        sta $0305,x ; health
        clc
        txa
        lsr
        adc #$20
        sta $0307,x ; OAM ref
        
   	rts
        
        
  
;;;; HANDLING BIRB
        
birb_cycle: subroutine
	ldx enemy_handler_pos
        ldy enemy_temp_oam_x
        ; update pattern
        lda ENEMY_RAM+3,x
        inc ENEMY_RAM+3,x
        inc ENEMY_RAM+3,x
        lda ENEMY_RAM+3,x
        
        ; set x position
        ; get x pattern position
        ; add it to base x position
        ; save that to OAM x position
        lda ENEMY_RAM+3,x
        tax
        lda sine_table,x
        lsr
        lsr
        lsr
	ldx enemy_handler_pos
        inc ENEMY_RAM+1,x
        clc
        adc ENEMY_RAM+1,x
        sta $0203,y
        sta collision_0_x
        
        ; set y position
        lda ENEMY_RAM+3,x
        clc 
        adc #$40
        tax
        lda sine_table,x
        lsr
        lsr
        lsr
	ldx enemy_handler_pos
        clc
        adc ENEMY_RAM+2,x
        sta $0200,y
        sta collision_0_y
        
        ; current sprite
        lda ENEMY_RAM+4,x
        clc
        adc #$14
        sta ENEMY_RAM+4,x
        lsr
        lsr
        lsr
        lsr
        lsr
        lsr
        clc 
        adc #$2c
        sta $0201,y
        lda #$08
        sta collision_0_w
        lda #$05
        sta collision_0_h
; shot by bullet?
        jsr player_bullet_collision_handler
        cmp #$00
        beq .birb_not_hit
        ; decrease health
	ldx enemy_handler_pos
        dec ENEMY_RAM+5,x
        lda ENEMY_RAM+5,x
        cmp #$00
        bne .birb_not_dead
.birb_is_dead
	inc phase_kill_count
	; give points
        lda #05
        jsr player_add_points_00
        ; change it into crossbones!
        lda #$01
        sta ENEMY_RAM+0,x
.birb_not_dead
        ; palette
        lda #$00
        sta $0202,y
        jmp .birb_done
.birb_not_hit
        ; palette
        lda #$01
        sta $0202,y
.birb_done
	jsr player_collision_detect
	jmp update_enemies_handler_next
        