
; counts in ranges that match numerical tiles

timer_update: subroutine
; UPDATE TIMER DIGITS
; but not if game is over
	lda phase_end_game
        bne .timer_done
; frames 1s
	inc timer_frames_1s
        lda #char_set_offset+$a
        cmp timer_frames_1s
        bne .timer_done
        lda #char_set_offset
        sta timer_frames_1s
; frames 10s
	inc timer_frames_10s
        lda #char_set_offset+$6
        cmp timer_frames_10s
        bne .timer_done
        lda #char_set_offset
        sta timer_frames_10s
; seconds 1s
	inc timer_seconds_1s
        lda #char_set_offset+$a
        cmp timer_seconds_1s
        bne .timer_done
        lda #char_set_offset
        sta timer_seconds_1s
; seconds 10s
	inc timer_seconds_10s
        lda #char_set_offset+$6
        cmp timer_seconds_10s
        bne .timer_done
        lda #char_set_offset
        sta timer_seconds_10s
; minutes 1s
	inc timer_minutes_1s
        lda #char_set_offset+$a
        cmp timer_minutes_1s
        bne .timer_done
        lda #char_set_offset
        sta timer_minutes_1s
; minutes 10s
	; we really checking this?
        ; let it go into tile limbo
        ; XXX GAME OVER at 99:59.59 !!
	inc timer_minutes_10s
.timer_done
	rts
        
        

timer_reset: subroutine
	lda #char_set_offset
        sta timer_frames_1s
        sta timer_frames_10s
        sta timer_seconds_1s
        sta timer_seconds_10s
        sta timer_minutes_1s
        sta timer_minutes_10s
        rts